buildscript {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.1"
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "androidx.navigation.safeargs"

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/configuration.gradle"

// CI
def isCi = "true" == System.getenv("CI")
def preDexEnabled = "true" == System.getProperty("pre-dex", "true")

android {
  resourcePrefix config.resourcePrefix

  compileSdkVersion config.compileSdkVersion
  buildToolsVersion config.buildToolsVersion

  defaultConfig {
    minSdkVersion config.minSdkVersion
    targetSdkVersion config.targetSdkVersion
    versionCode config.versionCode
    versionName versionName()

    def package_name = "net.yslibrary.historian"
    def filesAuthorityValue = package_name + '.files'
    def taskAffinityValue = package_name + '.task'
    manifestPlaceholders = [
        files_authority: filesAuthorityValue,
        task_affinity  : taskAffinityValue
    ]

    buildConfigField("String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\"")
    buildConfigField("String", "TASK_AFFINITY", "\"${taskAffinityValue}\"")
    buildConfigField("Boolean", "isOp", "true")
    buildConfigField("Boolean", "notification", "true")
  }

  buildTypes {
    debug {

    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  dexOptions {
    // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isCi
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  buildFeatures {
    dataBinding = true
    viewBinding = true
  }
}

dependencies {
  implementation 'androidx.recyclerview:recyclerview:1.1.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
  implementation "com.squareup.okio:okio:2.9.0"
  implementation "com.google.android.material:material:${deps.material}"
  implementation "androidx.annotation:annotation:${deps.annotation}"

  //lifecycle
  implementation "androidx.lifecycle:lifecycle-extensions:${deps.lifecycle}"
  implementation "androidx.lifecycle:lifecycle-viewmodel:${deps.lifecycle}"
  implementation "androidx.lifecycle:lifecycle-livedata:${deps.lifecycle}"

  //navigation
  implementation "androidx.navigation:navigation-fragment:${deps.navigation}"
  implementation "androidx.navigation:navigation-ui:${deps.navigation}"

  //noinspection AnnotationProcessorOnCompilePath
  compileOnly "org.projectlombok:lombok:${deps.lombok}"
  annotationProcessor "org.projectlombok:lombok:${deps.lombok}"

  //android room
  implementation "androidx.room:room-runtime:${deps.room}"
  implementation "androidx.room:room-rxjava2:${deps.room}"
  annotationProcessor "androidx.room:room-compiler:${deps.room}"

  //parceler
  implementation "org.parceler:parceler-api:${deps.parceler}"
  annotationProcessor "org.parceler:parceler:${deps.parceler}"

  //RxJava
  implementation "io.reactivex.rxjava2:rxjava:${deps.rxjava}"
  implementation "io.reactivex.rxjava2:rxandroid:${deps.rxandroid}"
  implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.1'

  //navigation
  implementation "androidx.navigation:navigation-fragment:${deps.navigation}"
  implementation "androidx.navigation:navigation-ui:${deps.navigation}"
}
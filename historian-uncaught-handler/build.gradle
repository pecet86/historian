apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/configuration.gradle"

// CI
def isCi = "true" == System.getenv("CI")
def preDexEnabled = "true" == System.getProperty("pre-dex", "true")

android {
  resourcePrefix config.resourcePrefix

  compileSdkVersion config.compileSdkVersion
  buildToolsVersion config.buildToolsVersion

  defaultConfig {
    minSdkVersion config.minSdkVersion
    targetSdkVersion config.targetSdkVersion
    versionCode versionCode()
    versionName versionName()

    consumerProguardFiles 'proguard-rules.pro'
  }

  dexOptions {
    // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isCi
  }

  compileOptions {
    // Flag to enable support for the new language APIs
    //coreLibraryDesugaringEnabled true
    //https://developer.android.com/studio/write/java8-support-table
    sourceCompatibility = config.sourceCompatibility
    targetCompatibility = config.targetCompatibility
  }

  buildFeatures {
    dataBinding true
    viewBinding true
  }

  lintOptions {
    abortOnError false
  }

  sourceSets {
    main.java.srcDirs += 'src/main/java'
  }
}

dependencies {
  implementation "androidx.appcompat:appcompat:${deps.appcompat}"
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation "com.google.android.material:material:${deps.material}"

  api project(':historian-core')

  implementation "androidx.annotation:annotation:${deps.annotation}"

  //noinspection AnnotationProcessorOnCompilePath
  compileOnly "org.projectlombok:lombok:${deps.lombok}"
  annotationProcessor "org.projectlombok:lombok:${deps.lombok}"

  //parceler
  implementation "org.parceler:parceler-api:${deps.parceler}"
  annotationProcessor "org.parceler:parceler:${deps.parceler}"
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
ext.versionCode = {
  def versionProps = getVersionProps()
  int major = versionProps['major'] as int
  int minor = versionProps['minor'] as int
  int patch = versionProps['patch'] as int
  return major * 1000000 + minor * 1000 + patch
}

ext.versionName = {
  def versionProps = getVersionProps()
  return "${versionProps['major']}.${versionProps['minor']}.${versionProps['patch']}"
}

ext.bumpVersionName = { boolean patch = false, boolean minor = false, boolean major = false ->
  def versionProps = getVersionProps()
  if (patch) {
    return "${versionProps['major']}.${versionProps['minor']}.${(versionProps['patch'] as int) + 1}"
  } else if (minor) {
    return "${versionProps['major']}.${(versionProps['minor'] as int) + 1}.0"
  } else if (major) {
    return "${(versionProps['major'] as int) + 1}.0.0"
  } else {
    throw new RuntimeException("nic nie wybrano")
  }
}

Properties getVersionProps() {
  def versionPropsFile = file("${project.rootDir}/gradle/version/version.properties")
  Properties versionProps = new Properties()
  versionProps.load(new FileInputStream(versionPropsFile))
  return versionProps
}
